buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.5'
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
idea.module.inheritOutputDirs = true

sourceSets {
  main {
    java {
      srcDir 'src/main/java'
      srcDir 'src/gameanalytics'
    }
    resources {
      srcDir 'src/main/resources'
    }
  }
}

repositories {
    mavenCentral()
    maven { // Waila
        name "profmobius"
        url "http://mobiusstrip.eu/maven"
    }
    maven { // NEI/CCC
        name 'chickenbones'
        url "http://chickenbones.net/maven/"
    }
    maven { // Chisel 2
        name 'chisel'
        url "http://coloredlightscore.us.to/maven/chisel-2"
    }
    maven { // OpenComputers
        name 'opencomputers'
        url "http://maven.cil.li"
    }
    ivy { // Buildcraft
        name "BuildCraft"
        artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision]-dev.[ext]"
    }
    ivy { // Remain In Motion
        name "RemainInMotion"
        artifactPattern "http://addons-origin.cursecdn.com/files/2251/630/[module]-[revision].[ext]"
    }
    ivy { // PlanetguyLib (For RiM)
        name "PlanetguyLib"
        artifactPattern "http://addons-origin.cursecdn.com/files/2255/580/[module]-[revision].[ext]"
    }
}

dependencies {
    compile name: "buildcraft", version: "${buildcraft_version}"
    compile name: "PlanetguyLib", version: "${pglib_version}"
    compile name: "RemainInMotion", version: "${rim_version}"
    compile "li.cil.oc:OpenComputers:MC${minecraft_version}-${opencomputers_version}-dev:dev"

    compile "com.cricketcraft.chisel:Chisel2:${chisel_version}:deobf"
    // Oh cool, it doesn't find CTMLib. Freaking useless.
    compile "com.cricketcraft.ctmlib:CTMLib:${ctmlib_version}:deobf"

    compile "mcp.mobius.waila:Waila:${waila_version}:dev"
    // Apparently transitive dependency resolution doesn't want to find CCC
    compile "codechicken:CodeChickenCore:${minecraft_version}-${codechickencore_version}:dev"
    compile "codechicken:CodeChickenLib:${minecraft_version}-${codechickenlib_version}:dev"
    // Or freaking NEI. Thanks a lot codechicken.
    compile "codechicken:NotEnoughItems:${minecraft_version}-${nei_version}:dev"
}

if (System.env.BUILD_NUMBER != null) mod_version = "$mod_version.$System.env.BUILD_NUMBER"

group = 'lordmau5.ffs'
version = "$minecraft_version-$mod_version"
archivesBaseName = "Fancy Fluid Storage"

sourceCompatibility = 1.7
targetCompatibility = 1.7

minecraft {
    runDir = "runtime/client/assets"
    version = "$minecraft_version-$forge_version"
    mappings = "snapshot_20160110"
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar, dependsOn: 'jar') {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives sourceJar, deobfJar
}

curseforge {
    apiKey=System.env.CURSE_API_KEY != null ? "$System.env.CURSE_API_KEY" : ''
    project {
      id='232683'
      changelog="https://github.com/Lordmau5/FFS/compare/$System.env.GIT_PREVIOUS_COMMIT...$System.env.GIT_COMMIT"
      releaseType='alpha'
      mainArtifact(jar) {
        displayName="FFS v$project.mod_version"
      }
      addArtifact deobfJar
    }
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.5'
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'

sourceSets {
  main {
    java {
      srcDir 'src/main/java'
    }
    resources {
      srcDir 'src/main/resources'
    }
  }
}

idea {
  module.inheritOutputDirs = true
}

repositories {
    mavenCentral()
    maven { // Waila
        name "profmobius"
        url "http://mobiusstrip.eu/maven"
    }
    maven { // OpenComputers
        name 'opencomputers'
        url "http://maven.cil.li"
    }
    maven { // Tesla
        name 'epoxide'
        url "http://maven.epoxide.xyz/"
    }
    maven { // Chisel / tterrag's maven
        name 'tterrag'
        url "http://maven.tterrag.com/"
    }
    flatDir {
      dirs 'libs'
    }
}

dependencies {
    compile name: "industrialcraft-2-2.5.1-ex19-dev"

    compile "team.chisel:Chisel:MC${minecraft_version}-${chisel_version}"

    compile "li.cil.oc:OpenComputers:MC${minecraft_version}-${opencomputers_version}-dev"

    compile "mcp.mobius.waila:Waila:${waila_version}:dev"

    compile "net.darkhax.tesla:Tesla:${minecraft_version}-${tesla_version}"
}

if (System.env.BUILD_NUMBER != null) mod_version = "$mod_version-b$System.env.BUILD_NUMBER"

group = 'lordmau5.ffs'
version = "$minecraft_version-$mod_version"
archivesBaseName = "Fancy Fluid Storage"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    runDir = "runtime/client"
    version = "$minecraft_version-$forge_version"
    mappings = "snapshot_20160604"
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar, dependsOn: 'jar') {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives sourceJar, deobfJar
}

curseforge {
    apiKey=System.env.CURSE_API_KEY != null ? "$System.env.CURSE_API_KEY" : ''
    project {
      id='232683'
      changelog="https://github.com/Lordmau5/FFS/compare/$System.env.GIT_PREVIOUS_COMMIT...$System.env.GIT_COMMIT"
      releaseType='alpha'
      mainArtifact(jar) {
        displayName="FFS v$project.mod_version"
      }
    }
}
